==caves_main
The passageway down is smooth, a perfect circle of darkness descending into the earth, the walls scraped clean as though by the passage of some great serpent.

Nyrna stand at the breach and lifts her hand high, her palm cupped around a small flame.

"Ready when you are Oath-breaker." She says. #image.nyrna 

The illumination from her small flame scatters light across the walls, reflecting in the facets of black rock embedded there like shards of ancient glass. 

*[...]

-Soon the one passage branches, then branches again. It would be easy for one to lose their way down here. A moments inattention, one thoughtless step, and you could find yourself trapped in an endless spiral of passageways with no hope of any succour.

You are not faint hearted, but you find yourself very aware of the sound of your breathing in the darkness, and the steady thrum of your own heart, so loud to your own ears that for a moment you wonder if perhaps there is another walking in the shadows alongside you.

You cast your gaze back over your shoulder, and light scatters across the cave walls in a dizzying cascade.

Is that movement you glimpse? A looming shadow? Are those footsteps trailing off into the distance?

* [Steel your mind - the Underworld is full of illusions and dark magic.]
{alterAbility(Mind, 1)}
* [Prepare yourself to fight: Skadi's snakes were known to make these caves their home.]
{alterAbility(Body, 1)}

- "Byleistr."#image.nyrna

The sound of your name brings you to your senses. How long has it been since she last called you that? How many times are you likely to hear it spoken again.

*["I'm here."] You find yourself saying. #image.player

- What you mean to say is that you are fine. That you haven't gone mad. That you aren't a child, afraid of the dark. But what use are such words between old friends. She knows all that already.
{Follower_stack>=9:
And so far, you have proven you can hold your own.
{alterAbility(Luck, 1)}
}
"This way." She says, and offers up a reassuring smile. #image.nyrna

*[...] #image.none

-After what feels like aeons, the cave opens up, and you find yourself in a vast underground cavern. Stalactite's hang suspended from the ceiling, vast draperies of wax-like stone dripping downwards to merge with the great stone columns that line the cave floor, like the pillars of some ancient hall. 

You see many more cave entrances lining the cavern walls, like a gallery of open mouths, waiting to be fed.

As your eyes adjust you notices the mushrooms blooming across moist patches of stone, their pale caps glowing faintly in the darkness.

"Someone has plucked these recently." Nyrna observes, pointing at some disturbance in the fungal growth. "Sigyn, perhaps?" #image.nyrna

By all accounts your brother is tightly bound, confined to a cave somewhere within this vast maze.

You could pass him by.

Perhaps you should. After all, no good can come of your meeting one another again after all this time. 

And yet you find yourself unable to move forward.

Sigyn will know where he is, and so Sigyn you must find.

It’s your last chance to see him. Your last chance to say goodbye.

*[...]

-You follow the signs, and it doesn't take long to come across her abode. The cave where she has made her home is small and damp. Scraps of mouldering cloth line the corner in a makeshift bed, and she has made a small fire-pit in the centre of the cave - though you can't for the life of you imagine what fuel she could find to light it down here.

As you poke experimentally through the coals, she comes up behind you, silent as a spirit. The first warning you have of her arrival is the sensation of cold steel pressed against the soft hollow of your throat.

Even Nyrna looks surprised.

*["You're still fond of knives I see."] You say, careful not to make any sudden movements. #image.player

-"And I see you're still fond of entering peoples homes without invitation, brother-in-law." #image.sigyn

It takes her an awfully long time to remove the knife from your throat, and when you turn to face her she regards you with the air of a wary animal - just as likely to bite you as befriend you.

Her face is shrouded under a hood, but you can see that her stay in these caves has left its mark on her. She has become gaunt and pale, and the her eyes glow with an unnatural light like two pale moons.

//probably need to work in some dialogue choices somewhere around here to make this a bit more engaging. Many many words. Much waffling

Sensing your scrutiny, she averts her gaze, and bends down to busy herself at the fireplace, pushing the coals back and forth with no clear purpose.

"Why did you come here after all this time?" She asks, sullen, accusing, hopeful. #image.sigyn

#image.none
You feel the taste of bitter shame rise in your throat. You were so concerned with your own troubles that you never considered what it must be like for her. 

Not that you were aware of the fullness of Loki's punishment, Grimnir was always careful to keep the truth hidden. But you'd known something was wrong.

Your family picked off one by one. Cast aside. Banished. Slaughtered. And you didn't even notice.

You could say all these things to her. You never really liked Sigyn, but for better or worse she is family, and you owe her at least that much. 

But instead you make a noncommittal grunt and look away.

Off to one side you hear Nyrna make a quiet sound of contempt. You can hardly blame her.

To think, once you were called brave, and songs of your exploits were sung in the halls of Vallalla by heroes of every Age.

"I suppose that <i>woman</i> told you I was down here." She says eventually, misinterpreting your silence, or perhaps just seeking to fill it. #image.sigyn

#image.player
She can't have many people to converse with down here. But then again, your brother did always enjoy the sound of his own voice. Loki silver-tongue, sharp of wit and sharp of word, bound by blood to Odin himself.

How far you've all come from those glorious days.

You wonder. Does he still talk so freely? Can he talk at all? Or did they cut out his tongue as part of his punishment. You wouldn't put it past them, The Aesir always excel at cruelty.

Baldr was the one exception. //le gasp, we speak his name. Or think it, whatever.

*["Angr–."] You start to speak her name, then think better of it. #image.player
"No, she did not mention you."

Sigyn scoffed, hitting the coals as if they were the heads of Loki's many bastard children.

**["I'm sorry it took so long for me to come here."]

Too little too late. You are always too late to make a difference.

She treats your words with the contempt they deserve, and instead gestures towards the end of the cave with the tip of her knife.

**["Where is he?"]

She glares at you, a flash of baleful moonlight, then shrugs and gestures towards the end of the cave with the tip of her knife. 

-"That way." She says, hunching her shoulders. #image.sigyn

You open your mouth to say something, <i>anything</i> more, but Nyrna steps towards you with a firm shake of her head. 

*[Go to Loki's cave.]->lokis_cave

=lokis_cave

You find your brother bound and broken, arms and ankles bound in iron chains that extend to three great stones of binding. The cave smells of old blood, iron, and fear. 

Faithful Sigyn has rigged a makeshift shelter to catch and divert the acidic droplets falling from the cavern roof, but you can see the etches it has left in the stone, and the places where it has burned your brothers flesh away.

You have never seen him like this, and he will never see again.

#image.loki
Loki's eyes are covered by a piece of ragged cloth, but you can see the edges of the scars underneath. He sits quietly upon a great slab of rock, knees pressed to his chest, feet bare and ringed with scars where the manacles have left their mark over the many days of his imprisonment.

He tilts his head to one side as you approach, listening to your footfalls.

"A new visitor! My, my, today <i>is</i> an auspicious day. Tell me your name stranger, it has been a long time since I last laid eyes upon a new face."

Loki laughs, catching his own mistake,  "I suppose it will be longer still before I lay eyes on one again. But my ears still work fine, rest assured."

"A name?" You say the words out loud before you can catch yourself. "Call me Nameless then, if you must, or Nidingr, or Vargr. All are equally appropriate."

"Then you are in fine company my friend. We are outcasts both, though I still have my name to call on." Again you catch that birdlike tilt to his head as he considers you from afar. 

"There is something familiar about your voice stranger, perhaps we knew each other once?"

*["Once, perhaps."] #image.player
{alterAbility(Luck, 1)}
"But no longer?" Loki asks. "What caused this sad parting of ways?" #image.Loki

You consider his question carefully. #image.player

**["Fate."] You say at last. 

"A man's fate can be altered, even unravelled if he has the will to do such a thing." #image.Loki

"Are you sure we cannot be friends? I may be bound to this cave,' Loki lifts his arms, heavy chains rattling against stone by way of demonstration. 'but there are ways around that." 

*["You are mistaken."]
{alterAbility(Mind, 1)}
 You offer up the lie so easily, and he accepts it. Your sharp brother who never let a single thing get past him, just allows your lie to rest between you without challenge.
 
 -That is when you see the runestone, glowing, nestled softly in an alcove behind Loki.
 
 "So what brings you here then, stranger?" #image.loki

You do not answer, instead you hurry towards the stone. He shifts towards you, and you realize suddenly that behind his glib words and bravado lies fear. He is afraid you are another torment, sent by the Aesir.

You have to answer him, somehow.
~temp succeeded = false
* [{RequireAbility(Mind, 5)} "I am not your enemy."] #image.player
{AbilityCheck(Mind, 10):
You tell him that, and he believes you. You watch as he relaxes into his seat.
~succeeded = true
-else:
He does not seem to believe you, and as you pass him by he tenses even more. {alterAbility(Mind, -1)}
}
 * [{RequireAbility(Body,4)} Make your footsteps silent.] #image.player
 {AbilityCheck(Body, 8):
 You move, silent like a cat, past him. He does not seem to notice.
 ~succeeded=true
 -else:
 Your foot hits a rock, and Loki suddenly cowers, raising his hands as best he can. Your heart splinters, but you do not stop. {alterAbility(Body, -1)}
 }
 * ["I am not here to torment you."] #image.player
 {AbilityCheck(Luck, 10):
 Somehow, he seems to believe you, despite it all. He relaxes as you pass him by.
 ~succeeded=true
 "So they all say." Loki mumbles. He tenses as you pass him by. {alterAbility(Luck, -1)}
 }
- ->runestone(succeeded)

=runestone(succeeded)
You are glad your brother cannot see what you are doing, or read the words before you.
->runestone_main->
Once the runestone is gone, you turn back to your brother - only to find him no longer alone. Sigyn is there, emptying the contraption above his head that keeps the poison from him. #image.sigyn

"You will want to leave. Go to Hel's fortress, I imagine?" She says, holding the bowl of acid. "Follow me then."

You follow her, away from Loki's cave, where he moans softly in terror as the drops fall around him. #image.loki

The cave opens up to a view of the void. Ginnungagap, the void that was before the world, the void that will be there when the world ends once more. Hoar and rimefrost cover it, and the roots of the world-tree reach into it, creating a makeshift bridge across. #image.none

{succeeded:
She turns to you after pouring the acid into the depths. "Thank you, for not frightening my husband. His torments are quite enough on their own." #image.sigyn

She presses something into your hands. "I have held on to this for too long. Perhaps you will know what to do with it."
{alterItem(Vial, 1)}
"Good luck, brother-in-law. May you find what you came for."
-else:
Sigyn pours the acid into the depths, then points. "Good luck, brother-in-law. And do not come back."
}

And she is gone. Before you stands the Ginnungagap.

* [{RequireItem(Feather)} You have a feather from the cloak of Freyja to aid you.]
The feather alone is not enough to make you fly, but with it in hand, you jump and skip from root to ice-floe, until you make it across. {alterAbility(Mind, 1)} {alterAbility(Body, 1)}{alterAbility(Luck,1)}{alterAbility(Follower, 1)}
->leave
* [You have no choice but to try to make it.]
{AbilityCheck(Body, 10):
You are beaten by ice-cold winds from the void, jump across gaps over an eternal fall, slip on frost that has been here since Ymir was yet whole. But you make it.
->leave
- else:
You make it far enough that you could not turn around, and then you slip and fall. You tumble towards the void. {AbilityDamage(Body, Medium)}
{AbilityCheck(Mind, 10):
You refuse to panic. You grab hold of a dead root and stop yourself from going over. For a moment you hang there, letting your breath calm, and then you pull yourself back up.
->leave
- else:
All you can think of is falling endlessly. What happens to those who fall into the Ginnungagap? Do they die? Or do they spend eternity in darkness? {AbilityDamage(Mind, Medium)}
{AbilityCheck(Luck, 10):
You see the branch in the dark at the last moment, hidden under the lip of the ice. You aim your feet at it, hit, and then push with all your might. Suddenly your fall has become a jump, a mighty jump, mighty enough to bridge the void itself. You shout defiance at the Norns as you land on solid ground again.
->leave
- else:
As you hurtle towards the void, your quest lost, you suddenly see a glow before you. Nyrna. {AbilityDamage(Luck, Medium)}
->leaveWithHelp
}
}
}

=leaveWithHelp
{AbilityCheck(Follower, 1):
Nyrna lifts you up as you fall, and for a moment you are gently rocked in the arms of Fate. She deposits you on the opposite bank.
"Why not ask for my help from the start, you fool?" Nyrna curses. #image.nyrna
}
She pretends not to be affected by it, but you can tell rescuing you took a lot out of her. {alterAbility(Follower, -1)}
->leave

=leave
~allowedLocation=Location4
* [Go to Hel.]
{Move(Location4)}
->travel_main.location4